import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '2.7.17'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'org.jetbrains.kotlin.jvm' version '1.6.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21'
	id 'org.openapi.generator' version '7.1.0'
	id 'org.jetbrains.kotlin.plugin.lombok' version '1.9.20'
	id 'io.freefair.lombok' version '8.1.0'
}

group = 'com.wanderers'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '11'
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation 'org.jetbrains.kotlin:kotlin-reflect'
//	implementation 'org.projectlombok:lombok:1.18.26'

	implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.8'
	implementation group: 'io.swagger.core.v3', name: 'swagger-models', version: '2.2.8'
	implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.7.0'

	implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.3'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

openApiGenerate {
	generatorName = "kotlin-spring"
	validateSpec = true
	inputSpec = "$projectDir/src/main/resources/static/hotelier-webservices-v1.yaml"
	outputDir = "$buildDir/generated/openapi"
	apiPackage = "com.wanderers.hotelier_webservices.rest.api"
	modelPackage = "com.wanderers.hotelier_webservices.rest.model"
	configOptions = [
			"dateLibrary": "java8",
			"hideGenerationTimestamp": "true",
			"delegatePattern": "true",
			"library": "spring-boot",
			"serializableModel": "true",
			"useBeanValidation": "true",
			"useTags": "true",
			"implicitHeaders": "true",
			"openApiNullable": "false"
	]
}

sourceSets {
	main {
		kotlin {
			srcDirs("$buildDir/generated/openapi/src/main/kotlin")
		}
	}
}

tasks.withType(KotlinCompile) {
	dependsOn(tasks.openApiGenerate)
}

task buildZip(type: Zip) {
	from compileKotlin
	from processResources
	into('lib') {
		from configurations.compileClasspath
	}
}

build.dependsOn buildZip

springBoot {
	mainClass = 'com.wanderers.hotelier_webservices.HotelierWebservicesApplication'
}

tasks.named('test') {
	useJUnitPlatform()
}
